plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    def versionFile = file("version.properties")
    final def PROP_NAME = "BUILD_NUMBER"
    if (!versionFile.canRead()) versionFile.createNewFile()
    def props = new Properties()
    props.load(versionFile.newInputStream())
    def buildNum = 1
    if (!props.isEmpty()) {
        buildNum = props[PROP_NAME].toInteger() + 1
    }
    props[PROP_NAME] = buildNum.toString()
    props.store(versionFile.newWriter(), null)
    defaultConfig {
        applicationId "uz.axrorxoja.notes"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode buildNum
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        register("release").configure {

            def signingFile = file("releaseConfig.properties")
            def signingProp = new Properties()
            signingProp.load(signingFile.newInputStream())
            println file(signingProp["STORE_FILE"]).path
            storeFile = file(signingProp["STORE_FILE"])
            storePassword = signingProp["STORE_PASSWORD"]
            keyAlias = signingProp["KEY_ALIAS"]
            keyPassword = signingProp["KEY_PASSWORD"]

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    def room_version = "2.2.5"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"
}